program = _{
    SOI ~ statement* ~ EOI
}
statement = _{
    SEPARATOR
  | schema_statement
  | language_statement
  | meta_statement
}
/*====================================================================================================================*/
schema_statement =  {"schema" ~ SYMBOL ~ inherit? ~ object}
meta_statement =  {SYMBOL ~ object}
language_statement =  {"language" ~ SYMBOL ~ inherit? ~ object}
/*====================================================================================================================*/
///Red
inherit = {":" ~ namespace}
object_inherit = {namespace? ~ object}
/*====================================================================================================================*/
object =  {"{" ~ (SEPARATOR | pair)* ~ "}"}
pair   =  {SYMBOL ~ Set ~ (object_inherit | list)}
list   =  {(!(SYMBOL ~ Set) ~ value)+}
Set    = _{"="|":"}
/*====================================================================================================================*/
value   = !{String|Special|Color|Number|namespace}
///#C678DD
Special = @{"true"|"false"|"null"}
///#E5C07B
Color   = @{"#" ~ ASCII_HEX_DIGIT+}
/*====================================================================================================================*/
///#D19A66
Number       = ${Sign? ~ (Decimal|Integer) ~ SYMBOL?}
Decimal      = ${Integer ~ "." ~ ("_"? ~ ASCII_DIGIT)*}
Integer      = @{ASCII_DIGIT ~ ("_"? ~ ASCII_DIGIT)*}
Sign         = @{"+"|"-"}
/*====================================================================================================================*/
///#98C379
String    = ${"\"" ~ Character* ~ "\""}
Character = @{
    "\\u" ~ ASCII_HEX_DIGIT{4}
  | "\\u" ~ "{" ~(ASCII_HEX_DIGIT|SPACE_SEPARATOR)+ ~ "}"
  | "\\" ~ ANY
  | (!("\"") ~ ANY)+
}
/*====================================================================================================================*/
///#61AFEF
namespace =  {SYMBOL ~ ("." ~ SYMBOL)*}
SYMBOL    = @{XID_START ~ (XID_CONTINUE|"_"|"-")*}
/*====================================================================================================================*/
///Gray
COMMENT          = _{"//" ~ (!NEWLINE ~ ANY)*}
WHITESPACE       = _{NEWLINE|SPACE_SEPARATOR|"\t"}
SEPARATOR        = _{","|";"}
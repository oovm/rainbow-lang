program = _{
    SOI ~ statement* ~ EOI
}
statement = _{
    SEPARATOR
  | EmptyLine{1,}
  | schema_statement
}
EmptyLine  = @{WHITESPACE* ~ NEWLINE}
/*====================================================================================================================*/
import_statement =  {"import" ~ String}
schema_statement =  {"schema" ~ SYMBOL ~ object}
meta_statement =  {SYMBOL ~ object}
global_statement =  {"global" ~ object}
language_statement =  {"language" ~ SYMBOL ~ language_inherit? ~ object}
/*====================================================================================================================*/
language_inherit = {":" ~ SYMBOL}
object_inherit = {namespace? ~ object}
/*====================================================================================================================*/
object =  {"{" ~ (SEPARATOR | pair)* ~ "}"}
pair   =  {SYMBOL ~ Set ~ (object_inherit | list)}
list   =  {(!(SYMBOL ~ Set) ~ value)+}
Set    = _{"="|":"}
/*====================================================================================================================*/
value = !{String|Special|Color|Number|SYMBOL}
Special = @{"true"|"false"|"null"}
Color   = @{"#" ~ ASCII_HEX_DIGIT+}
/*====================================================================================================================*/
//!#D19A66: SignedNumber
Number       = ${SignedNumber ~ SYMBOL?}
SignedNumber = ${Sign? ~ (Decimal|DecimalBad|Integer)}
Decimal      = ${Integer ~ "." ~ ("_"? ~ ASCII_DIGIT)*}
DecimalBad   = ${Integer ~ "."|"." ~ ("_"? ~ ASCII_DIGIT)*}
//Integer    = @{"0"|ASCII_NONZERO_DIGIT ~ ("_"? ~ ASCII_DIGIT)*}
Integer      = @{ASCII_DIGIT ~ ("_"? ~ ASCII_DIGIT)*}
Sign         = @{"+"|"-"}
/*====================================================================================================================*/
//!#3C963C: String|InlineString|InlineString2|RestLineText
//!#98C379: StringQuotation|StringAccent
//!#D19A66: StringApostrophe
String    = @{"\"" ~ Character* ~ "\""}
Character = @{
    "\\u" ~ ASCII_HEX_DIGIT{4}
  | "\\u" ~ "{" ~(ASCII_HEX_DIGIT|SPACE_SEPARATOR)+ ~ "}"
  | "\\" ~ ANY
  | (!("\"") ~ ANY)+
}
/*====================================================================================================================*/
///#61AFEF
namespace =  {SYMBOL ~ ("." ~ SYMBOL)*}
SYMBOL    = @{XID_START ~ (XID_CONTINUE|"_"|"-")*}
/*====================================================================================================================*/
// NEWLINE = @{"\r" ~ "\n"|"\r"|"\n"}

///Gray
COMMENT          =  {MultiLineComment|LineComment}
WHITESPACE       = _{NEWLINE|SPACE_SEPARATOR|"\t"}
LineComment      = ${"//" ~ (!NEWLINE ~ ANY)*}
MultiLineComment = ${"/*" ~ (MultiLineComment | !"*/" ~ ANY)* ~ "*/"}

SEPARATOR  = @{","|";"}